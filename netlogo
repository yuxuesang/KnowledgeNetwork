breed [customers customer]
breed [servants servant]

turtles-own
[
  strategy
  absorb-rate
  forget-rate
  innovation-rate
  kv       ;;knowledge-vector
  old-kv
  
  node-clustering-coefficient
  distance-from-other-turtles   ;; list of distances of this node from other turtles
]

links-own
[
  rewired?                    ;; keeps track of whether the link has been rewired or not
]

globals
[
  total-kv
  total-mean-kv
  total-mean-old-kv
  servant-mean-kv
  servant-mean-old-kv
  customer-mean-kv
  customer-mean-old-kv
  total-sd
  customer-sd
  servant-sd
  sd-mean
  increase-rate
  customer-increase-rate
  servant-increase-rate
  
  
  interval
  clustering-coefficient               ;; the clustering coefficient of the network; this is the                                  ;; average of clustering coefficients of all turtles
  average-path-length                  ;; average path length of the network
  clustering-coefficient-of-lattice    ;; the clustering coefficient of the initial lattice
  average-path-length-of-lattice       ;; average path length of the initial lattice
  infinity                             ;; a very large number.
                                         ;; used to denote distance between two turtles which
                                         ;; don't have a connected or unconnected path between them
  highlight-string                     ;; message that appears on the node properties monitor
  number-rewired                       ;; number of edges that have been rewired. used for plots.
  rewire-one?                          ;; these two variables record which button was last pushed
  rewire-all?
]

;;;;;;;;;;;;;;;;;;;;;;;;
;;; Setup Procedures ;;;
;;;;;;;;;;;;;;;;;;;;;;;;

to startup
  set highlight-string ""
end

to setup
  ca
  set infinity 999999999999999  ;; just an arbitrary choice for a large number
  set-default-shape turtles "circle"
  make-turtles

  ;; set up a variable to determine if we still have a connected network
  ;; (in most cases we will since it starts out fully connected)
  let success? false
  while [not success?] [
    ;; we need to find initial values for lattice
    wire-them
    ;;calculate average path length and clustering coefficient for the lattice
    set success? do-calculations
  ]

  ;; setting the values for the initial lattice
  set clustering-coefficient-of-lattice clustering-coefficient
  set average-path-length-of-lattice average-path-length
  set number-rewired 0
  set highlight-string ""
  
  update-visuals
  update-globals
 ;; update-plotting
end

to make-turtles
  random-seed 100
  crt num-nodes [ 
    set color gray + 2 
    setxy random-xcor random-ycor
   ; set kv random-float 1 + 0.0001
  ]
    ask n-of (num-nodes * servants-rate) turtles   [
      set breed servants
    ] 
    ask turtles [
      if breed != servants [
      set breed customers
      ]
   ]
    
  
  ask servants [
  set color green
  set absorb-rate (random-float  0.1 + 0.01)
  set forget-rate random-float 1
  set kv random-float 1 + 0.0001
  set innovation-rate (random-float 0.1 + 0.01)
   ifelse random-float 1.0 < servant-c [
      set strategy "c"
    ][ set strategy "d"
    ]
  
]
    
ask customers [
  set color pink
  
  set absorb-rate random-float 0.1
  set forget-rate random-float 1
  set innovation-rate random-float 0.1
  ifelse (random-float 1.0 < expert-rate) [
      set kv 3
    ][set kv (random  1 + 0.1)
    ]
  ifelse random-float 1.0 < customer-c [
      set strategy "c"
    ][ set strategy "d"
    ]
]
  ;; arrange them in a circle in order by who number
  ;;layout-circle (sort turtles) max-pxcor - 1
 
  
end

to update-visuals
  ask turtles [ update-node-appearance ]
  
end

to update-globals
  if (total-kv < infinity) [set total-kv sum [kv] of turtles]
  if (total-kv < infinity) [set total-mean-kv mean [kv] of turtles]
  if (total-kv < infinity) [set total-mean-old-kv mean [old-kv] of turtles]
  if (count servants > 0 and total-kv < infinity) [ set servant-mean-kv mean [kv] of servants]
  if (count customers > 0 and total-kv < infinity)[ set customer-mean-kv mean [kv] of customers]
  if ( any? turtles and total-kv < infinity ) [set total-sd standard-deviation [kv] of turtles ]
  if ( any? turtles and total-kv < infinity ) [set customer-sd standard-deviation [kv] of customers ]
  if ( any? turtles and total-kv < infinity ) [set servant-sd standard-deviation [kv] of servants ]
  ;;set sd-mean total-sd / mean-kv
  if (total-kv < infinity and total-mean-old-kv > 0)  [set increase-rate ( total-mean-kv / total-mean-old-kv - 1 )]
  if (total-kv < infinity and servant-mean-old-kv > 0)  [set servant-increase-rate ( servant-mean-kv / servant-mean-old-kv - 1 )]
  if (total-kv < infinity and customer-mean-old-kv > 0)  [set customer-increase-rate ( customer-mean-kv / customer-mean-old-kv - 1 )]
  
end


to update-node-appearance ; node procedure
  ; scale the size to be between 0.1 and 5.0
  set size 0.1 + 5 * sqrt (kv / (total-kv + 0.00001))
end


to go 
  if ticks > 100 [stop]
  communication
  update-visuals
  update-globals
  if (ticks mod strategy-update-rate = 0) [update-strategy]
  ;;update-strategy
  if (ticks mod links-update-rate = 0) [update-links]
  tick
  update-plotting
  
end

to update-plotting
  set-current-plot "Mean kv"
  set-current-plot-pen "totals"
  plot total-mean-kv
  set-current-plot-pen "servants"
  plot servant-mean-kv
  set-current-plot-pen "customers"
  plot customer-mean-kv
  
  set-current-plot "sd-mean"
  set-current-plot-pen "totals"
  plot total-sd / total-mean-kv
  set-current-plot-pen "servants"
  plot servant-sd / servant-mean-kv
  set-current-plot-pen "customers"
  plot customer-sd / customer-mean-kv
  
  set-current-plot "increase-rate"
  set-current-plot-pen "totals"
  plot increase-rate
  ;set-current-plot-pen "servants"
  ;plot servant-increase-rate
  ;set-current-plot-pen "customers"
  ;plot customer-increase-rate
  
  set-current-plot "c-rate"
  set-current-plot-pen "totals"
  plot count turtles with [strategy = "c"] / count turtles
  set-current-plot-pen "servants"
  plot count servants with [strategy = "c"] / count servants
  set-current-plot-pen "customers"
  plot count customers with [strategy = "c"] / count customers
  
  
end 
 
to  communication
  ask turtles [set old-kv kv]
  ask turtles [
    ;ifelse ( ticks mod 2 = 0) [
    set kv  (1 - forget-rate) * kv * (1 + innovation-rate)
    let recipients (link-neighbors with [kv < [kv] of myself] )
    if any? recipients [
      ask recipients [
        ;;if (kv < [kv] of myself) [
          ;;set kv (kv + absorb-rate * ([kv] of myself - kv)  )
          if ( ([strategy] of myself = "c" ) and strategy = "c" )
            [ set kv (kv + absorb-rate * ([kv] of myself - kv)  ) ] 
          if (([strategy] of myself = "c"  ) and (strategy = "d") )
            [ set kv (kv + absorb-rate * 0.8 * ([kv] of myself - kv)  )  ] 
          if (([strategy] of myself = "d" ) and (strategy = "c" ) )
            [ set kv (kv + absorb-rate * (([kv] of myself) * 0.7 - kv)  ) ] 
          if (([strategy] of myself = "d" ) and (strategy = "d") )
            [ set kv (kv + absorb-rate * 0.8 * (([kv] of myself ) * 0.7 - kv)  ) ] 
            
        ]
      ]
    ]
      
end

to update-strategy
    ask turtles [
      let imitated-ones (link-neighbors with [kv > [kv] of myself] )
      if any? imitated-ones[
      
        let imitated-one (max-one-of imitated-ones [kv])
        let s  ([strategy] of imitated-one)
         set strategy s
      ]
    ]
end



to update-links
  ask min-n-of 5 turtles [kv] [
     ask one-of my-links [die]
     create-link-with one-of turtles with [kv > mean [kv] of turtles]
     [set color blue]
    ]
    
  
end


;;;;;;;;;;;;;;;;;;;;;;;
;;; Main Procedure ;;;
;;;;;;;;;;;;;;;;;;;;;;;

to rewire-one

  ;; make sure num-turtles is setup correctly else run setup first
  if count turtles != num-nodes [
    setup
  ]

  ;; record which button was pushed
  set rewire-one? true
  set rewire-all? false

  let potential-edges links with [ not rewired? ]
  ifelse any? potential-edges [
    ask one-of potential-edges [
      ;; "a" remains the same
      let node1 end1
      ;; if "a" is not connected to everybody
      if [ count link-neighbors ] of end1 < (count turtles - 1)
      [
        ;; find a node distinct from node1 and not already a neighbor of node1
        let node2 one-of turtles with [ (self != node1) and (not link-neighbor? node1) ]
        ;; wire the new edge
        ask node1 [ create-link-with node2 [ set color cyan  set rewired? true ] ]

        set number-rewired number-rewired + 1  ;; counter for number of rewirings

        ;; remove the old edge
        die
      ]
    ]
    ;; plot the results
    let connected? do-calculations
    do-plotting
  ]
  [ user-message "all edges have already been rewired once" ]
end

to rewire-all

  ;; make sure num-turtles is setup correctly; if not run setup first
  if count turtles != num-nodes [
    setup
  ]

  ;; record which button was pushed
  set rewire-one? false
  set rewire-all? true

  ;; set up a variable to see if the network is connected
  let success? false

  ;; if we end up with a disconnected network, we keep trying, because the APL distance
  ;; isn't meaningful for a disconnected network.
  while [not success?] [
    ;; kill the old lattice, reset neighbors, and create new lattice
    ask links [ die ]
    wire-them
    set number-rewired 0

    ask links [

      ;; whether to rewire it or not?
      if (random-float 1) < rewiring-probability
      [
        ;; "a" remains the same
        let node1 end1
        ;; if "a" is not connected to everybody
        if [ count link-neighbors ] of end1 < (count turtles - 1)
        [
          ;; find a node distinct from node1 and not already a neighbor of node1
          let node2 one-of turtles with [ (self != node1) and (not link-neighbor? node1) ]
          ;; wire the new edge
          ask node1 [ create-link-with node2 [ set color cyan  set rewired? true ] ]

          set number-rewired number-rewired + 1  ;; counter for number of rewirings
          set rewired? true
        ]
      ]
      ;; remove the old edge
      if (rewired?)
      [
        die
      ]
    ]

    ;; check to see if the new network is connected and calculate path length and clustering
    ;; coefficient at the same time
    set success? do-calculations
  ]

  ;; do the plotting
  do-plotting
end

;; do-calculations reports true if the network is connected,
;;   and reports false if the network is disconnected.
;; (In the disconnected case, the average path length does not make sense,
;;   or perhaps may be considered infinite)
to-report do-calculations

  ;; set up a variable so we can report if the network is disconnected
  let connected? true

  ;; find the path lengths in the network
  find-path-lengths

  let num-connected-pairs sum [length remove infinity (remove 0 distance-from-other-turtles)] of turtles

  ;; In a connected network on N nodes, we should have N(N-1) measurements of distances between pairs,
  ;; and none of those distances should be infinity.
  ;; If there were any "infinity" length paths between nodes, then the network is disconnected.
  ;; In that case, calculating the average-path-length doesn't really make sense.
  ifelse ( num-connected-pairs != (count turtles * (count turtles - 1) ))
  [
      set average-path-length infinity
      ;; report that the network is not connected
      set connected? false
  ]
  [
    set average-path-length (sum [sum distance-from-other-turtles] of turtles) / (num-connected-pairs)
  ]
  ;; find the clustering coefficient and add to the aggregate for all iterations
  find-clustering-coefficient

  ;; report whether the network is connected or not
  report connected?
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Clustering computations ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to-report in-neighborhood? [ hood ]
  report ( member? end1 hood and member? end2 hood )
end


to find-clustering-coefficient
  ifelse all? turtles [count link-neighbors <= 1]
  [
    ;; it is undefined
    ;; what should this be?
    set clustering-coefficient 0
  ]
  [
    let total 0
    ask turtles with [ count link-neighbors <= 1]
      [ set node-clustering-coefficient "undefined" ]
    ask turtles with [ count link-neighbors > 1]
    [
      let hood link-neighbors
      set node-clustering-coefficient (2 * count links with [ in-neighborhood? hood ] /
                                         ((count hood) * (count hood - 1)) )
      ;; find the sum for the value at turtles
      set total total + node-clustering-coefficient
    ]
    ;; take the average
    set clustering-coefficient total / count turtles with [count link-neighbors > 1]
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Path length computations ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Implements the Floyd Warshall algorithm for All Pairs Shortest Paths
;; It is a dynamic programming algorithm which builds bigger solutions
;; from the solutions of smaller subproblems using memoization that
;; is storing the results.
;; It keeps finding incrementally if there is shorter path through
;; the kth node.
;; Since it iterates over all turtles through k,
;; so at the end we get the shortest possible path for each i and j.

to find-path-lengths
  ;; reset the distance list
  ask turtles
  [
    set distance-from-other-turtles []
  ]

  let i 0
  let j 0
  let k 0
  let node1 one-of turtles
  let node2 one-of turtles
  let node-count count turtles
  ;; initialize the distance lists
  while [i < node-count]
  [
    set j 0
    while [j < node-count]
    [
      set node1 turtle i
      set node2 turtle j
      ;; zero from a node to itself
      ifelse i = j
      [
        ask node1 [
          set distance-from-other-turtles lput 0 distance-from-other-turtles
        ]
      ]
      [
        ;; 1 from a node to it's neighbor
        ifelse [ link-neighbor? node1 ] of node2
        [
          ask node1 [
            set distance-from-other-turtles lput 1 distance-from-other-turtles
          ]
        ]
        ;; infinite to everyone else
        [
          ask node1 [
            set distance-from-other-turtles lput infinity distance-from-other-turtles
          ]
        ]
      ]
      set j j + 1
    ]
    set i i + 1
  ]
  set i 0
  set j 0
  let dummy 0
  while [k < node-count]
  [
    set i 0
    while [i < node-count]
    [
      set j 0
      while [j < node-count]
      [
        ;; alternate path length through kth node
        set dummy ( (item k [distance-from-other-turtles] of turtle i) +
                    (item j [distance-from-other-turtles] of turtle k))
        ;; is the alternate path shorter?
        if dummy < (item j [distance-from-other-turtles] of turtle i)
        [
          ask turtle i [
            set distance-from-other-turtles replace-item j distance-from-other-turtles dummy
          ]
        ]
        set j j + 1
      ]
      set i i + 1
    ]
    set k k + 1
  ]

end

;;;;;;;;;;;;;;;;;;;;;;;
;;; Edge Operations ;;;
;;;;;;;;;;;;;;;;;;;;;;;

;; creates a new lattice
to wire-them
  ;; iterate over the turtles
  let n 0
  while [n < count turtles]
  [
    ;; make edges with the next two neighbors
    ;; this makes a lattice with average degree of 4
    make-edge turtle n
              turtle ((n + 1) mod count turtles)
    make-edge turtle n
              turtle ((n + 2) mod count turtles)
    make-edge turtle n
              turtle ((n + 3) mod count turtles)
    set n n + 1
  ]
end

;; connects the two turtles
to make-edge [node1 node2]
  ask node1 [ create-link-with node2  [
    set rewired? false
  ] ]
end

;;;;;;;;;;;;;;;;
;;; Graphics ;;;
;;;;;;;;;;;;;;;;

to highlight
  ;; remove any previous highlights
  ask turtles [ set color gray + 2 ]
  ask links [ set color gray + 2 ]
  if mouse-inside? [ do-highlight ]
  display
end

to do-highlight
  ;; getting the node closest to the mouse
  let min-d min [distancexy mouse-xcor mouse-ycor] of turtles
  let node one-of turtles with [count link-neighbors > 0 and distancexy mouse-xcor mouse-ycor = min-d]
  if node != nobody
  [
    ;; highlight the chosen node
    ask node
    [
      set color pink - 1
      let pairs (length remove infinity distance-from-other-turtles)
      let local-val (sum remove infinity distance-from-other-turtles) / pairs
      ;; show node's clustering coefficient
      set highlight-string (word "clustering coefficient = " precision node-clustering-coefficient 3
                                 " and avg path length = " precision local-val 3
                                 " (for " pairs " turtles )")
    ]
    let neighbor-nodes [ link-neighbors ] of node
    let direct-links [ my-links ] of node
    ;; highlight neighbors
    ask neighbor-nodes
    [
      set color blue - 1

      ;; highlight edges connecting the chosen node to its neighbors
      ask my-links [
        ifelse (end1 = node or end2 = node)
        [
          set color blue - 1 ;
        ]
        [
          if (member? end1 neighbor-nodes and member? end2 neighbor-nodes)
            [ set color yellow ]
        ]
      ]
    ]
  ]
end

;;;;;;;;;;;;;;;;
;;; Plotting ;;;
;;;;;;;;;;;;;;;;

to do-plotting

   if rewire-one? [
     ;; plot the rewire-one graph
     set-current-plot "Network Properties Rewire-One"
     set-current-plot-pen "apl"
     ;; note: dividing by value at initial value to normalize the plot
     plotxy number-rewired / count links
            average-path-length / average-path-length-of-lattice

     set-current-plot-pen "cc"
     ;; note: dividing by initial value to normalize the plot
     plotxy number-rewired / count links
            clustering-coefficient / clustering-coefficient-of-lattice
   ]

   if rewire-all? [
     ;; plot the rewire-all graph
     set-current-plot "Network Properties Rewire-All"
     set-current-plot-pen "apl"
     ;; note: dividing by value at initial value to normalize the plot
     plotxy rewiring-probability
            average-path-length / average-path-length-of-lattice

     set-current-plot-pen "cc"
     ;; note: dividing by initial value to normalize the plot
     plotxy rewiring-probability
            clustering-coefficient / clustering-coefficient-of-lattice
          

   ]
end



